(ns clojure.core-test.cycle
  (:require [clojure.test :refer [deftest testing are is]]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists cycle
  (deftest test-cycle

    (testing "nominal cases"
      (are [n coll expected] (= expected (take n (cycle coll)))
        1 nil []
        1 '() []
        1 '(1 2 3) [1]
        3 '(1 2 3) [1 2 3]
        7 '(1 2 3) [1 2 3 1 2 3 1]
        3 (range) [0 1 2]  ; TODO Phel hangs (probably counts until integer overflow)
        ;; Basilisp does not currently implement sorted collections.
        #?@(:lpy [] :default [7 (sorted-set 1 2 3) [1 2 3 1 2 3 1]]))

      ;; Map iteration order in Basilisp is not guaranteed. This is effectively
      ;; the same test, only accounting for both potential iteration orders.
      #?(:lpy (is (contains? #{[[:a 1] [:b 2] [:a 1]]
                               [[:b 2] [:a 1] [:b 2]]}
                             (vec (take 3 (cycle {:a 1 :b 2})))))
         :default (is (= [[:a 1] [:b 2] [:a 1]] (take 3 (cycle {:a 1 :b 2}))))))

    (testing "bad shape"
      (are [coll] (p/thrown? (cycle coll))
                  :k
                  42
                  3.14))))

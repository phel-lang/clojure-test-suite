(ns clojure.core-test.pop-bang
  (:require [clojure.test :refer [are deftest is testing]]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists pop!
  (deftest test-pop!

    (testing "nominal cases"
      (are [expected vec] (= expected (persistent! (pop! (transient vec))))
                          [] [nil]
                          [] [1]
                          [1 2] [1 2 3]
                          [:c :b] [:c :b :a]))

    (testing "cannot pop! empty vector"
      (is (p/thrown? (pop! (transient [])))))

    ;; Basilisp does not prevent continuing to use transient vectors after persistent! call
    #?@(:lpy []
        :default
        [(testing "cannot pop! after call to persistent!"
           (let [t (transient [0 1]), _ (persistent! t)]
             (is (p/thrown? (pop! t)))))])

    (testing "bad shapes"
      (are [arg] (p/thrown? (pop! arg))
                 (transient {:a 0})
                 (transient #{0})
                 [0]
                 '(0)
                 #{0}
                 (range 3)
                 true
                 false
                 "s"
                 3.14
                 42
                 nil))))

(ns clojure.core-test.pos-qmark
  (:require [clojure.test :as t :refer [are deftest is]]
            [clojure.core-test.number-range :as r]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists pos?
  (deftest test-pos?
    (are [expected x] (= expected (pos? x))
      false 0
      true  1
      false -1
      false 0.0
      true  1.0
      false -1.0
      true  r/min-double
      true  r/max-double
      true  ##Inf
      false ##-Inf
      false ##NaN
      false 0N
      true  1N
      false -1N
      false 0.0M
      true  1.0M
      false -1.0M

      ;; Python VMs integer types are arbitrary precision and have no min or max.
      #?@(:lpy []
          :default
          [false r/min-int
           true  r/max-int])

      #?@(:cljs []
          :default
          [false 0/2
           true  1/2
           false -1/2]))

    ;; Phel divergence: numeric predicate lenient on bad input; nil/false
    ;; return false, true coerces to 1 (positive) instead of throwing
    ;; (Bucket A/B, #2223).
    #?@(:phel
        [(is (= false (pos? nil)))
         (is (= false (pos? false)))
         (is (= true (pos? true)))]
        :cljs
        [(is (not (pos? nil)))
         (is (not (pos? false))) ; Prints warning
         (is (pos? true))] ; Prints warning
        :lpy
        [(is (p/thrown? (pos? nil)))
         (is (not (pos? false)))
         (is (pos? true))]
        :default
        [(is (p/thrown? (pos? nil)))
         (is (p/thrown? (pos? false)))
         (is (p/thrown? (pos? true)))])))
